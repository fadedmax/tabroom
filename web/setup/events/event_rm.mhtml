<%args>
	$person
	$event_id
	$tourn
	$fire_as_function => undef
	$certain          => undef
</%args>
<%init>

	my $event = Tab::Event->retrieve($event_id);

	if ($event
		&& $event->tourn == $tourn
		&& ($fire_as_function || $certain eq "Delete ".$event->abbr)
	) {

		my $description = "User ".$person->first." ".$person->last." has deleted event ".$event->id." ".$event->name;

		$m->comp("/funclib/log.mas",
			type        => 'tabbing',
			event       => $event->id,
			tourn       => $tourn->id,
			person      => $person->id,
			description => $description
		);

		my $dbh = Tab::DBI->db_Main();

		my $score_sth = $dbh->prepare("
			delete score.*
			from score, ballot, panel, round
			where round.event = ?
			and round.id = panel.round
			and panel.id = ballot.panel
			and ballot.id = score.ballot
		");

		$score_sth->execute($event->id);

		my $ballot_sth = $dbh->prepare("
			delete ballot.*
			from ballot, panel, round
			where round.event = ?
			and round.id = panel.round
			and panel.id = ballot.panel
		");

		$ballot_sth->execute($event->id);

		my $panel_sth = $dbh->prepare("
			delete panel.*
				from panel, round
			where round.event = ?
				and round.id = panel.round
		");

		$panel_sth->execute($event->id);

		foreach my $other ('entry_setting', 'entry_student', 'qualifier', 'rating', 'strike', 'result') {

			my $meta_sth = $dbh->prepare("
				delete $other.*
					from (entry, $other)
				where entry.event = ?
					and entry.id = $other.entry
			");

			$meta_sth->execute($event->id);
			$meta_sth->finish();
		}


		foreach my $other ('round', 'result_set') {
			my $other_sth = $dbh->prepare("
				delete from $other where event = ?
			");

			$other_sth->execute($event->id);
		}


		$event->delete();

		return if $fire_as_function;
		$m->redirect("/setup/events/edit.mhtml");
	}

</%init>

	<div class="blankfull centeralign">

	<form action="event_rm.mhtml" method="post">

		<input
			type  = "hidden"
			value = "<% $event_id %>"
			name  = "event_id"
		>

		<& "/funclib/warning.mas", person => $person &>

		<h6 class="semibold centeralign martopmore bluetext">
			You are about to permanently delete <% $event->name %>.
		</h6>

		<p class="semibold bluetext padvertmore bigger">
			You will lose all <% scalar($event->rounds) %> rounds and
			<% scalar($event->entries) %> entries.
		</p>

		<p class="bigger padvertmore">
			This is the type of thing you probably want to
			<a
				class  = "plain inline semibold redtext bigger"
				href   = "/register/data/export_data.mhtml?event_id=<% $event->id %>"
				target = "_blank"
			>take a data backup <span class='inline fa fa-sm fa-external-link-square'></span></a>
			before doing.  <br />Otherwise, this action cannot be undone.  No, really.  Please be very certain.
		</p>

% 		if ($certain) {
			<p class="redtext semibold bigger">
				You did not type "Delete <% $event->abbr %>", exactly like that.  Try again.
			</p>
% 		}

		<p>To proceed, type "Delete <% $event->abbr %>" in the box below:</p>

		<div class="centeralign martopmore">
			<span class='third'>
				<input
					type           = "text"
					name           = "certain"
					size           = "32"
					autocomplete   = "off"
					autocorrect    = "off"
					autocapitalize = "off"
					spellcheck     = "false"
					class          = "martop notfirst"
				>
			</span>
		</div>

		<p class="centeralign padvertmore">
			<input
				type  = "submit"
				value = "Yes I am sure I know what I am doing"
			>
		</p>

	</form>

	</div>

