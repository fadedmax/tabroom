<%args>
	$id    => undef
	$key   => undef
	$pass1 => undef
	$pass2 => undef
</%args>
<%init>

	$m->abort unless $id;
	$m->abort unless $key;
	$m->abort unless $pass1;
	$m->abort unless $pass2;

	my $now = DateTime->now();
	my $target = Tab::Person->retrieve($id);

	unless ($target) {
		$m->comp("/funclib/abort.mas", message => "Invalid account ID sent");
	}

	my %target_settings = $target->all_settings();
	my $err;

	my $dbh = Tab::DBI->db_Main();

	my $wipe_sth = $dbh->prepare("
		delete ps.* from person_setting ps
		where ps.person = ?
		and ps.tag IN ('pass_change_expires', 'pass_changekey')
	");

	unless ($target_settings{"pass_change_expires"} && $target_settings{"pass_changekey"}) {

		$err = "No valid password change key found";
		$wipe_sth->execute($target->id);

	} elsif ($target_settings{"pass_change_expires"}
		&& $target_settings{"pass_change_expires"}->epoch < $now->epoch
	) {

		$err = "No valid password change key found";
		$wipe_sth->execute($target->id);

	} elsif ($target_settings{"pass_changekey"} != $key) {

		$err = "No valid password change key found";
		$wipe_sth->execute($target->id);

	} elsif ($pass1 ne $pass2) {

		$err = "The two passwords you entered did not match";

	} elsif ($target_settings{force_password_change}) {

		my $db_sha_crypt = $target->password;
		my $sha_crypt = crypt($pass1, $db_sha_crypt) if $db_sha_crypt;

		my $pass_ok;

		if ($sha_crypt && ($sha_crypt eq $db_sha_crypt)) {
			$pass_ok++;
		}

		if ($pass_ok) {
			$err = "You entered your current password; for this change you must select a new one."
		}
	}

	$m->redirect("/user/login/forgot.mhtml?id=$id&key=$key&err=$err") if $err;

	my $salt = $m->comp("/funclib/generate_randomstring.mas");
	my $new_sha2_hash = crypt($pass1, '$6$' . $salt);

	$wipe_sth->execute($target->id);

	$target->password($new_sha2_hash);
	$target->update();

	my $current = $ARGS{"person"};

	if ($current > 0 && $current->site_admin && $current != $target) {
		$target->setting('force_password_change', 1);
	} else {
		$target->setting('force_password_change', 0);
	}

	my $pass_change_sth = $dbh->prepare("
		update person set person.pass_timestamp = NOW() where person.id = ?
	");

	$pass_change_sth->execute($target->id);

	Tab::log(
		"PASSWORD CHANGE: Person ID ".$target->id." (".$target->email.") "
		.$target->first." ".$target->last.
		" password was changed from IP address ".$ENV{HTTP_X_FORWARDED_FOR}.
		" using a forgotten password link "
	);

	my $msg = "Your password has been changed.  You may now log in.";
	my $subject = "Your Tabroom.com password has been changed";
	my $email = "\n\tHello! \n";

	$email .= "The password for your Tabroom.com account has been changed ";
	$email .= "If you did not attempt to make this change, please notify us at ";
	$email .= 'help@tabroom.com immediately. ';
	$email .= "\n\n";

	$email .= "Thank you!\n";
	$email .= "Tabroom.com, a service of the National Speech & Debate Assocation\n";
	$email .= "https://www.tabroom.com\n";
	$email .= "https://www.speechanddebate.org\n";

	my @raw;
	push @raw, $target->id;

	my $numberwang = substr($now->epoch, -6, 6);
	my $from_string = "Tabroom Password Reset <iforgot_".$numberwang.'@www.tabroom.com>';

	$m->comp("/funclib/send_notify.mas",
		from     => $from_string,
		ids      => \@raw,
		no_inbox => 1,
		subject  => $subject,
		body     => $email
	);

	$m->redirect("/user/login/login.mhtml?username=".$target->email."&msg=$msg");

</%init>

