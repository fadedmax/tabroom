<%args>
	$person
	$perms
	$ballot_id      => undef
	$school_id      => undef
	$property_value => undef
</%args>
<%init>

	$m->clear_buffer();
	$r->content_type('application/json');

	unless ($ballot_id || $school_id) {
		$m->print('{ "error": true, "message": "No ballot ID sent"}');
		$m->abort();
	}

	my $dbh = Tab::DBI->db_Main();

	my %response = (
		error => \0
	);

	if ($ballot_id) {

		my $sth = $dbh->prepare("
			select
				ballot.id, ballot.entry, school.chapter
				from ballot, entry, school
			where ballot.id  = ?
				and ballot.entry = entry.id
				and entry.school = school.id
		");

		$sth->execute($ballot_id);
		my $results = $sth->fetchall_hash();
		my $ballot = shift @{$results};

		unless ($person->site_admin || $perms->{"chapter"}{$ballot->{chapter}}) {
			$m->print('{ "error": true, "message": "You do not have admin permissions to that school."}');
			$m->abort();
		}

		if ($property_value) {

			my $approve_sth = $dbh->prepare("
				update ballot set approved = ?, timestamp = timestamp where id = ?
			");

			$approve_sth->execute($person->id, $ballot_id);
			$response{'message'} = "Ballot marked as viewable to contestants by ".$person->email;

		} else {

			my $disapprove_sth = $dbh->prepare("
				update ballot set approved = NULL, timestamp = timestamp where id = ?
			");

			$disapprove_sth->execute($ballot_id);
			$response{'message'} = "Ballot marked as NOT viewable to contestants";
		}

	} else {

		my $sth = $dbh->prepare("
			select school.chapter
				from school
			where school.id  = ?
		");

		$sth->execute($school_id);
		my $results = $sth->fetchall_hash();
		my $res = shift @{$results};

		unless ($person->site_admin || $perms->{"chapter"}{$res->{chapter}}) {
			$m->print('{ "error": true, "message": "You do not have admin permissions to that school."}');
			$m->abort();
		}

		if ($property_value) {

			my $approve_ballot_sth = $dbh->prepare("
				update ballot, entry
					set ballot.approved = ?, ballot.timestamp = ballot.timestamp
				where 1=1
					and entry.school = ?
					and entry.id = ballot.entry
					and exists (
						select s2.id
						from score s2
						where s2.ballot = ballot.id
						and s2.tag IN ('rfd', 'comment')
					)
			");

			$approve_ballot_sth->execute($person->id, $school_id);
			$response{'message'} = "All ballots marked as viewable to your competitors.";
			$response{"refresh"} = \1;

		} else {

			my $disapprove_ballot_sth = $dbh->prepare("
				update ballot, entry
					set ballot.approved = NULL, ballot.timestamp = ballot.timestamp
				where 1=1
					and entry.school = ?
					and entry.id = ballot.entry
					and exists (
						select s2.id
						from score s2
						where s2.ballot = ballot.id
						and s2.tag IN ('rfd', 'comment')
					)
			");

			$disapprove_ballot_sth->execute($school_id);
			$response{'message'} = "No ballots marked as viewable to your competitors.";
			$response{"refresh"} = \1;
		}
	}

	$m->print(JSON::encode_json(\%response));
	$m->abort();

</%init>
