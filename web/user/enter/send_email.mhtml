<%args>
	$person
	$school
	$judges     => undef
	$entries    => undef
	$contacts   => undef
	$parents    => undef
	$send_to    => undef
	$registered => undef
	$pending    => undef
	$rejected   => undef
	$memo       => undef
	$entry_info => undef
	$subject    => undef
	$body       => undef
</%args>
<%init>

	my $tourn = $school->tourn;
	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;

	# Who is the email going to?

	my $dbh = Tab::DBI->db_Main();

	my @entries;

	if ($entries || $parents) {
		push @entries, $school->entries( unconfirmed => 0 ) if $registered;
		push @entries, $school->entries( unconfirmed => 1 ) if $pending;
		push @entries, $school->entries( unconfirmed => 2 ) if $rejected;
	}

	my %seen;
	@entries = grep { ! $seen{$_->id} ++ } @entries;

	if ($memo)  {

		my $signup_memo = $school->files(tag => "signup")->first;

		if ($signup_memo) {
			$body .= "\n\nMemo: ".$Tab::s3_url."/".$tourn->id."/signups/".$school->id."/".$signup_memo->filename."\n";
		}
	}

	if ($entry_info) {

		$body .= "\n\nTournament: \t".$tourn->name."\n";
		$body .= "Location: \t".$tourn->city." ".$tourn->state." ".$tourn->country."\n";

		if ($tourn->end->day != $tourn->start->day) {
			$body .= "Dates:\t ".Tab::nicefulldate($tourn->start->set_time_zone($tz))." to ";
			$body .= Tab::nicefulldate($tourn->end->set_time_zone($tz))."\n"
		} else {
			$body .= "Date: \t".Tab::nicefulldate($tourn->start->set_time_zone($tz))."\n";
		}

		$body .= "Website: \t http://".$tourn->webname.".tabroom.com\n";
	}

	my %ids;

	if ($contacts) {
		my $contacts  = $m->comp("/funclib/contacts.mas", school => $school, return => 1 );
		foreach my $ref (@{$contacts}) {
			$ids{$ref->{person_id}}++;
		}
	}

	if ($judges) {

		my $sth = $dbh->prepare("
			select distinct person.id
			from judge, person
			where judge.school = ?
			and judge.person = person.id
		");

		$sth->execute($school->id);

		my $results = $sth->fetchall_hash();

		foreach my $ref (@{$results}) {
			$ids{$ref->{id}}++;
		}
	}

	if ($entries || $parents) {

		foreach my $entry (@entries) {
			foreach my $student ($entry->students) {

				if ($entries && $student->person) {
					$ids{$student->person->id}++;
				}

				if ($parents) {
					foreach my $follower ($student->followers) {
						if ($follower->person) {
							$ids{$follower->person->id}++;
						}
					}
				}
			}
		}
	}

	unless ($subject) {
		$subject = "Message about ".$tourn->name;
	}

	$ids{$person->id}++;
	my @sendto = keys %ids;

	my $from_string = $person->first." ".$person->last." via Tabroom <chapter_".$school->chapter->id.'@tabroom.com>';
	my $reply_to = $person->email;

	my $reply = $m->comp("/funclib/send_notify.mas",
		from     => $from_string,
		reply_to => $reply_to,
		ids      => \@sendto,
		no_web   => 1,
		body     => $body,
		subject  => $subject,
	);

	my $msg = "Email sent to ".scalar @sendto." people";
	$m->redirect("emails.mhtml?school_id=".$school->id."&msg=$msg");

</%init>

